Функция ВыполнитьПриложение(ПараметрыПроцедуры, IDСообщения = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(IDСообщения) Тогда
		
		// это сделано для отправки сообщения вручную из очереди...
		ОбработатьОбработанное = Ложь;
		ПростаяПередачаПараметров = Ложь;
		wsОбработчики.ОбработатьСообщение(IDСообщения, ОбработатьОбработанное, ПростаяПередачаПараметров);
		Возврат Неопределено;		
	
	КонецЕсли; 
	
	Если ПараметрыПроцедуры.Свойство("Ссылка") И ПараметрыПроцедуры.Свойство("ТипСообщения") Тогда
		
		// запись преварительного сообщения в очередь (только ссылка), запуск обработки...
		
		ID = Неопределено;
		
		wsОбработчики.ЗаписатьСообщениеВОчередьСообщений(
				"Отправка не выполнялась",
					ПараметрыПроцедуры.ТипСообщения, 
						Перечисления.wsНаправлениеСообщения.Исходящее,ID,
							ПараметрыПроцедуры.Ссылка, Ложь); // не обработано
		
		// запускаем обработку...
		ПростаяПередачаПараметров = Ложь;
		ВключаяОбработанное = Ложь;
		wsОбработчики.ОбработатьСообщение(ID, ВключаяОбработанное, ПростаяПередачаПараметров);
		
	ИначеЕсли ПараметрыПроцедуры.Свойство("ПростаяПередачаПараметров")	Тогда 
		
		Если НЕ ПараметрыПроцедуры.Обработано Тогда
			
			// обработка 
			СформироватьИОтправитьСообщение(ПараметрыПроцедуры);
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецФункции

Процедура СформироватьИОтправитьСообщение(ПараметрыПроцедуры)
	
	// Таблица 44. Сопоставление реквизитов документа «ПеремещениеТоваров» (1С: ERP) и полей файла
	// Имя объекта источника (1С: ERP)	Имя поля файла XML	Комментарии
	// Имя свойства источника (1С: ERP)		
	// Документ «ПеремещениеТоваров»  		
	// ТЧ.Товары.Назначение.Заказ	Заказ  ordering	сейчас pp-заказ
	// ТЧ.Товары.Серия.Номер	Партия party	
	// ТЧ.Товары.Серия.АЭМ_НомерПлавки	Номер отливки numb_melt	
	// ТЧ.Товары.Серия.АЭМ_НомерПоковки 	Номер поковки numb_forg	
	//

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ПараметрыПроцедуры.СсылкаНаОбъект);    
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Назначения.Заказ.Номер КАК ordering,
		|	0 КАК position,
		|	"""" КАК production,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК date,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК dateend,
		|	"""" КАК status,
		|	"""" КАК nomenclatureCode,
		|	"""" КАК nomenclatureName,
		|	"""" КАК DrawingDesignation,
		|	0 КАК weight,
		|	"""" КАК customer_name,
		|	"""" КАК customer_code,
		|	"""" КАК material_num,
		|	"""" КАК material_weight,
		|	"""" КАК material_name,
		|	"""" КАК SteelGrade,
		|	СерииНоменклатуры.Номер КАК party,
		|	СерииНоменклатуры.АЭМ_НомерПлавки КАК numb_melt,
		|	СерииНоменклатуры.АЭМ_НомерПоковки КАК numb_forg
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
		|		ПО ПеремещениеТоваровТовары.Назначение = Назначения.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
		|		ПО ПеремещениеТоваровТовары.Серия = СерииНоменклатуры.Ссылка
		|ГДЕ
		|	ПеремещениеТоваровТовары.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	// каждую строку отправляем отдельным сообщением (так устроен приемник в МЕС)
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОтправитьСообщение(ВыборкаДетальныеЗаписи, ПараметрыПроцедуры);	
	КонецЦикла;
	

	
КонецПроцедуры

Процедура ОтправитьСообщение(Выборка, ПараметрыПроцедуры, Отказ = Ложь)
	
	
	wsНастройкиВебСервисов =  wsОбработчики.ОписаниеВебСервиса(ПараметрыПроцедуры.ТипСообщения);
	
	// Подключение
	Определения = Новый WSОпределения(wsНастройкиВебСервисов.МестоположениеWSDL, 
							wsНастройкиВебСервисов.ИмяПользователя, wsНастройкиВебСервисов.Пароль);
							
							
	ОписниеТипаЧисло = Новый ОписаниеТипов("Число");							
	position = ОписниеТипаЧисло.ПривестиЗначение(ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.position));							
							
	Попытка
		Прокси = Новый WSПрокси(Определения,
									wsНастройкиВебСервисов.URIПространстваИмен, 
											wsНастройкиВебСервисов.ИмяWEBСервиса, 
													wsНастройкиВебСервисов.ИмяТочкиПодключения,,60);
		Прокси.Пользователь = wsНастройкиВебСервисов.ИмяПользователя;
		Прокси.Пароль = wsНастройкиВебСервисов.Пароль;
		
		Результат = Прокси.PutOrders(
		XMLСтрока(Выборка.ordering),
		Выборка.position,
		XMLСтрока(Выборка.production),
		Выборка.date,
		Выборка.dateend,
		XMLСтрока(Выборка.status),
		XMLСтрока(Выборка.nomenclatureCode),
		XMLСтрока(Выборка.nomenclatureName),
		XMLСтрока(Выборка.DrawingDesignation),
		Выборка.weight,  // число
		XMLСтрока(Выборка.party),
		XMLСтрока(Выборка.numb_forg),
		XMLСтрока(Выборка.numb_melt),
		XMLСтрока(Выборка.customer_name),
		XMLСтрока(Выборка.customer_code),
		"", // ИНН
		"", // КПП
		XMLСтрока(Выборка.material_num),
		XMLСтрока(Выборка.material_weight),       // srtring
		XMLСтрока(Выборка.material_name),
		XMLСтрока(Выборка.SteelGrade)
		);
		
		Если Результат Тогда
			Сообщить("Готово.");
		Иначе
			ВызватьИсключение "Ошибка на стороне приемника.";
			Возврат;
		КонецЕсли; 
		
	Исключение
		Отказ = Истина;
		ВызватьИсключение "Ошибка: " + ОписаниеОшибки();
		Возврат;
	КонецПопытки;
	
	Текст = 
		"ordering " + Выборка.ordering + Символы.ПС +
		"position "  + Выборка.position + Символы.ПС +
		"production " + Выборка.production + Символы.ПС +
		"date " + Выборка.date + Символы.ПС +
		"status " + Выборка.status + Символы.ПС +
		"dateend " + Выборка.dateend + Символы.ПС +
		"nomenclatureCode " + Выборка.nomenclatureCode + Символы.ПС +
		"nomenclatureName " + Выборка.nomenclatureName + Символы.ПС +
		"DrawingDesignation " + Выборка.DrawingDesignation + Символы.ПС +
		"weight " + Выборка.weight + Символы.ПС +
		"party " + Выборка.party + Символы.ПС +
		"numb_forg " + Выборка.numb_forg + Символы.ПС +
		"numb_melt " + Выборка.numb_melt + Символы.ПС +
		"customer_name " + Выборка.customer_name + Символы.ПС +
		"customer_code " + Выборка.customer_code + Символы.ПС +
		"" + Символы.ПС + // ИНН
		"" + Символы.ПС + // КПП
		"material_num " + Выборка.material_num + Символы.ПС +
		"material_weight " + Выборка.material_weight + Символы.ПС +
		"material_name " + Выборка.material_name + Символы.ПС +
		"SteelGrade " + Выборка.SteelGrade;

	
	Если Не Отказ Тогда

		Попытка
			
			// Обновляем сообщение в регистре
			wsОбработчики.ЗаписатьСообщениеВОчередьСообщений(
				Текст, 
					ПараметрыПроцедуры.ТипСообщения, 
						Перечисления.wsНаправлениеСообщения.Исходящее,
							ПараметрыПроцедуры.IDСообщения,
								ПараметрыПроцедуры.СсылкаНаОбъект, 
									Истина, // обработано
										ПараметрыПроцедуры.Дата); 
		Исключение
			
			ВызватьИсключение "Ошибка: " + ОписаниеОшибки();
			
		КонецПопытки; 
	
	КонецЕсли; 
	

КонецПроцедуры
 
 